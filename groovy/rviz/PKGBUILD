pkgdesc="ROS - 3D visualization tool."
url='http://www.ros.org/'

pkgname='ros-groovy-rviz'
pkgver='1.9.33'
_micro=0
arch=('i686' 'x86_64')
pkgrel=1
license=('BSD, Creative Commons')
makedepends=('ros-build-tools')

depends=(ros-groovy-std-srvs
  ros-groovy-roscpp
  ros-groovy-tf
  ros-groovy-rosconsole
  ros-groovy-urdf-interface
  ros-groovy-sensor-msgs
  ros-groovy-geometry-msgs
  ros-groovy-image-geometry
  ros-groovy-pluginlib
  ros-groovy-urdf
  ros-groovy-std-msgs
  ros-groovy-rosbag
  ros-groovy-python-qt-binding
  ros-groovy-image-transport
  ros-groovy-message-filters
  ros-groovy-interactive-markers
  ros-groovy-roslib
  ros-groovy-visualization-msgs
  ros-groovy-laser-geometry
  ros-groovy-resource-retriever
  ros-groovy-rospy
  ros-groovy-nav-msgs
  eigen3
  ogre-1.8
  qt4
  assimp
  yaml-cpp0.3
  python2-pyqt
)

build() {
  [ -f /opt/ros/groovy/setup.bash ] && source /opt/ros/groovy/setup.bash

  if [ -d ${srcdir}/rviz ]; then
    cd ${srcdir}/rviz
    git fetch origin --tags
    git reset --hard release/groovy/rviz/${pkgver}-${_micro}
  else
    git clone -b release/groovy/rviz/${pkgver}-${_micro} git://github.com/ros-gbp/rviz-release.git ${srcdir}/rviz
  fi

  cd ${srcdir}/rviz

  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build


  # Adapt paths for Ogre 1.8
  export PKG_CONFIG_PATH="/opt/OGRE-1.8/lib/pkgconfig:$PKG_CONFIG_PATH"
  export LD_LIBRARY_PATH="/opt/OGRE-1.8/lib:$LD_LIBRARY_PATH"

  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/rviz

  cmake ${srcdir}/rviz -DCATKIN_BUILD_BINARY_PACKAGE=ON \
                       -DCMAKE_INSTALL_PREFIX=/opt/ros/groovy \
                       -DPYTHON_EXECUTABLE=/usr/bin/python2 \
                       -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
                       -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
                       -DSETUPTOOLS_DEB_LAYOUT=OFF \
                       -DCMAKE_LIBRARY_PATH="/opt/OGRE-1.8/lib"
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
