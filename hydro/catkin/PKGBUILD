
pkgdesc="ROS - Low-level build system macros and infrastructure for ROS."
url='http://www.ros.org/'

pkgname='ros-hydro-catkin'
pkgver='0.5.79'
_pkgver_patch=0
arch=('i686' 'x86_64')
pkgrel=6
license=('BSD')

ros_makedepends=()
makedepends=('cmake' 'git' 'ros-build-tools'
  ${ros_makedepends[@]}
  gtest
  python2
  python2-nose
  python2-empy)

ros_depends=()
depends=(${ros_depends[@]}
  cmake
  python2
  python2-catkin_pkg
  python2-empy
  python2-nose
  gtest)

_tag=release/hydro/catkin/${pkgver}-${_pkgver_patch}
_dir=catkin
source=("${_dir}"::"git+https://github.com/ros-gbp/catkin-release.git"#tag=${_tag})
md5sums=('SKIP')

build() {
  # Use ROS environment variables
  /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/hydro/setup.bash ] && source /opt/ros/hydro/setup.bash

  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Fix Python2/Python3 conflicts
  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/${_dir}

  # Build project
  cmake ${srcdir}/${_dir} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCATKIN_BUILD_BINARY_PACKAGE=OFF \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/hydro \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
