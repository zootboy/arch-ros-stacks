
pkgdesc="ROS - A framework for writing drivers that helps with runtime reconfiguration, diagnostics and self-test."
url='http://www.ros.org/'

pkgname='ros-hydro-driver-base'
pkgver='1.6.7'
_pkgver_patch=0
arch=('i686' 'x86_64')
pkgrel=1
license=('BSD')
makedepends=('cmake' 'ros-build-tools')

ros_depends=(ros-hydro-message-generation
  ros-hydro-message-runtime
  ros-hydro-diagnostic-updater
  ros-hydro-roscpp
  ros-hydro-self-test
  ros-hydro-dynamic-reconfigure
  ros-hydro-std-msgs)
depends=(${ros_depends[@]})

_tag=release/hydro/driver_base/${pkgver}-${_pkgver_patch}
_dir=driver_base
source=("${_dir}"::"git+https://github.com/ros-gbp/driver_common-release.git"#tag=${_tag})
md5sums=('SKIP')

build() {
  # Use ROS environment variables
  /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/hydro/setup.bash ] && source /opt/ros/hydro/setup.bash

  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Fix Python2/Python3 conflicts
  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/${_dir}

  # Build project
  cmake ${srcdir}/${_dir} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/hydro \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
