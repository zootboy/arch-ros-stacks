
pkgdesc="ROS - Robot-independent Gazebo plugins for sensors, motors and dynamic reconfigurable components."
url='http://www.ros.org/'

pkgname='ros-hydro-gazebo-plugins'
pkgver='2.4.1'
_pkgver_patch=0
arch=('i686' 'x86_64')
pkgrel=2
license=('BSD')
makedepends=('cmake' 'ros-build-tools')

ros_depends=(ros-hydro-rosconsole
  ros-hydro-sensor-msgs
  ros-hydro-rosgraph-msgs
  ros-hydro-angles
  ros-hydro-dynamic-reconfigure
  ros-hydro-diagnostic-updater
  ros-hydro-pcl-conversions
  ros-hydro-nodelet
  ros-hydro-driver-base
  ros-hydro-image-transport
  ros-hydro-geometry-msgs
  ros-hydro-gazebo-msgs
  ros-hydro-nav-msgs
  ros-hydro-roscpp
  ros-hydro-std-srvs
  ros-hydro-urdf
  ros-hydro-polled-camera
  ros-hydro-message-generation
  ros-hydro-cv-bridge
  ros-hydro-rospy
  ros-hydro-trajectory-msgs
  ros-hydro-tf
  ros-hydro-pcl-ros)
depends=(${ros_depends[@]}
  gazebo
  ogre-1.8)

_tag=release/hydro/gazebo_plugins/${pkgver}-${_pkgver_patch}
_dir=gazebo_plugins
source=("${_dir}"::"git+https://github.com/ros-gbp/gazebo_ros_pkgs-release.git"#tag=${_tag}
        "ogre_linker.patch")
md5sums=('SKIP'
         '995e16e3e777e9a4542d77654ecb1a54')

build() {
  # Use ROS environment variables
  /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/hydro/setup.bash ] && source /opt/ros/hydro/setup.bash

  # Apply patch
  msg "Patching source code"
  cd ${srcdir}/${_dir}
  git apply ${srcdir}/ogre_linker.patch

  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Fix Python2/Python3 conflicts
  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/${_dir}

  # Adapt paths for Ogre 1.8
  export PKG_CONFIG_PATH="/opt/OGRE-1.8/lib/pkgconfig:$PKG_CONFIG_PATH"
  export LD_LIBRARY_PATH="/opt/OGRE-1.8/lib:$LD_LIBRARY_PATH"

  # Build project
  cmake ${srcdir}/${_dir} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/hydro \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF \
        -DCMAKE_LIBRARY_PATH="/opt/OGRE-1.8/lib"
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
