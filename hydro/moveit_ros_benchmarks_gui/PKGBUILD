
pkgdesc="ROS - MoveIt GUI tools for benchmarking."
url='http://www.ros.org/'

pkgname='ros-hydro-moveit-ros-benchmarks-gui'
pkgver='0.5.12'
_pkgver_patch=0
arch=('i686' 'x86_64')
pkgrel=2
license=('BSD')
makedepends=('cmake' 'ros-build-tools')

ros_depends=(ros-hydro-moveit-ros-planning
  ros-hydro-eigen-conversions
  ros-hydro-moveit-ros-benchmarks
  ros-hydro-moveit-ros-warehouse
  ros-hydro-interactive-markers
  ros-hydro-moveit-ros-visualization
  ros-hydro-rviz
  ros-hydro-roscpp
  ros-hydro-tf)
depends=(${ros_depends[@]}
  ogre-1.8)

_tag=release/hydro/moveit_ros_benchmarks_gui/${pkgver}-${_pkgver_patch}
_dir=moveit_ros_benchmarks_gui
source=("${_dir}"::"git+https://github.com/ros-gbp/moveit_ros-release.git"#tag=${_tag}
        "moc_boost.patch")
md5sums=('SKIP'
         "255875ed9f6235c532c8823ec7483129")

build() {
  # Use ROS environment variables
  /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/hydro/setup.bash ] && source /opt/ros/hydro/setup.bash

  # Apply patch
  msg "Patching source code"
  cd ${srcdir}/${_dir}
  git apply ${srcdir}/moc_boost.patch

  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Fix Python2/Python3 conflicts
  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/${_dir}

  # Adapt paths for Ogre 1.8
  export PKG_CONFIG_PATH="/opt/OGRE-1.8/lib/pkgconfig:$PKG_CONFIG_PATH"
  export LD_LIBRARY_PATH="/opt/OGRE-1.8/lib:$LD_LIBRARY_PATH"

  # Build project
  cmake ${srcdir}/${_dir} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/hydro \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF \
        -DCMAKE_LIBRARY_PATH="/opt/OGRE-1.8/lib"
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
