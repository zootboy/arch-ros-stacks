
pkgdesc="ROS - OpenCV."
url='http://www.ros.org/'

pkgname='ros-hydro-opencv2'
pkgver='2.4.7'
_pkgver_patch=1
arch=('i686' 'x86_64')
pkgrel=2
license=('BSD')

ros_makedepends=()
makedepends=('cmake' 'git' 'ros-build-tools'
  ${ros_makedepends[@]}
  python2-numpy
  ffmpeg
  libtiff
  libjpeg-turbo
  zlib
  gtk2
  v4l-utils
  jasper
  python2-support
  libpng
  python2)

ros_depends=(ros-hydro-catkin)
depends=(${ros_depends[@]}
  python2-numpy
  ffmpeg
  libtiff
  libjpeg-turbo
  zlib
  gtk2
  v4l-utils
  jasper
  python2-support
  libpng
  python2)

_tag=release/hydro/opencv2/${pkgver}-${_pkgver_patch}
_dir=opencv2
source=("${_dir}"::"git+https://github.com/ros-gbp/opencv2-release.git"#tag=${_tag})
md5sums=('SKIP')

_cmakeopts=('-DWITH_CUDA=OFF'
            '-DWITH_OPENCL=ON'
            '-DWITH_OPENGL=ON'
            '-DWITH_QT=OFF'
            '-DWITH_TBB=ON'
            '-DWITH_V4L=ON'
            '-DWITH_XINE=ON'
            '-DENABLE_FAST_MATH=OFF'
            '-DBUILD_DOCS=OFF'
            '-DBUILD_PACKAGE=OFF'
            '-DBUILD_WITH_DEBUG_INFO=OFF'
            '-DBUILD_TESTS=OFF'
            '-DBUILD_PERF_TESTS=OFF'
            '-DBUILD_EXAMPLES=OFF'
            '-DBUILD_NEW_PYTHON_SUPPORT=ON'
            '-DINSTALL_C_EXAMPLES=ON'
            '-DINSTALL_PYTHON_EXAMPLES=ON'
            '-DCMAKE_SKIP_RPATH=ON')

# SSE only available from Pentium 3 onwards (i686 is way older)
[[ "$CARCH" = 'i686' ]] && \
  _cmakeopts+=('-DENABLE_SSE=OFF'
               '-DENABLE_SSE2=OFF'
               '-DENABLE_SSE3=OFF')

# all x64 CPUs support SSE2 but not SSE3
[[ "$CARCH" = 'x86_64' ]] && _cmakeopts+=('-DENABLE_SSE3=OFF')

build() {
  # Use ROS environment variables
  /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/hydro/setup.bash ] && source /opt/ros/hydro/setup.bash

  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Fix Python2/Python3 conflicts
  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/${_dir}

  # Build project
  cmake ${srcdir}/${_dir} ${_cmakeopts[@]} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/hydro \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF
  make
}

package() {
  # Fix error with opencv_ts
  options=('staticlibs')
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
