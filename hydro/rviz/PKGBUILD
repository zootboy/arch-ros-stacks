
pkgdesc="ROS - 3D visualization tool for ROS."
url='http://www.ros.org/'

pkgname='ros-hydro-rviz'
pkgver='1.10.10'
arch=('i686' 'x86_64')
pkgrel=2
license=('BSD, Creative Commons')
makedepends=('cmake' 'ros-build-tools')

ros_depends=(ros-hydro-rosconsole
  ros-hydro-resource-retriever
  ros-hydro-sensor-msgs
  ros-hydro-image-geometry
  ros-hydro-visualization-msgs
  ros-hydro-pluginlib
  ros-hydro-map-msgs
  ros-hydro-image-transport
  ros-hydro-python-qt-binding
  ros-hydro-geometry-msgs
  ros-hydro-roslib
  ros-hydro-laser-geometry
  ros-hydro-nav-msgs
  ros-hydro-std-msgs
  ros-hydro-urdfdom
  ros-hydro-rosbag
  ros-hydro-roscpp
  ros-hydro-std-srvs
  ros-hydro-urdf
  ros-hydro-message-filters
  ros-hydro-rospy
  ros-hydro-interactive-markers
  ros-hydro-tf
  ros-hydro-media-export)
depends=(${ros_depends[@]}
  qt4
  ogre-1.8
  assimp
  eigen3
  tinyxml
  yaml-cpp0.3)

_tag=release/hydro/rviz/${pkgver}-0
_dir=rviz
source=("${_dir}"::"git+https://github.com/ros-gbp/rviz-release.git"#tag=${_tag})
md5sums=('SKIP')

build() {
  # Use ROS environment variables
  /usr/share/ros-build-tools/clear-ros-env.sh
  [ -f /opt/ros/hydro/setup.bash ] && source /opt/ros/hydro/setup.bash

  # Create build directory
  [ -d ${srcdir}/build ] || mkdir ${srcdir}/build
  cd ${srcdir}/build

  # Fix Python3 error
  /usr/share/ros-build-tools/fix-python-scripts.sh ${srcdir}/${_dir}

  # Adapt paths for Ogre 1.8
  export PKG_CONFIG_PATH="/opt/OGRE-1.8/lib/pkgconfig:$PKG_CONFIG_PATH"
  export LD_LIBRARY_PATH="/opt/OGRE-1.8/lib:$LD_LIBRARY_PATH"

  cmake ${srcdir}/${_dir} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCATKIN_BUILD_BINARY_PACKAGE=ON \
        -DCMAKE_INSTALL_PREFIX=/opt/ros/hydro \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DSETUPTOOLS_DEB_LAYOUT=OFF \
        -DCMAKE_LIBRARY_PATH="/opt/OGRE-1.8/lib"
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}/" install
}
